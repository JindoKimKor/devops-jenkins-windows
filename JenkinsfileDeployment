def editMode = "EditMode"
def playMode = "PlayMode"
def util

pipeline {
    agent any

    parameters {
        string(name: 'PR_BRANCH', defaultValue: '', description: '')
        string(name: 'PR_REPO_HTML', defaultValue: '', description: '')
        string(name: 'PR_PROJECT', defaultValue: '', description: '')
        string(name: 'PR_STATE', defaultValue: '', description: '')
    }

    triggers {
        GenericTrigger(
            genericVariables: [
                [key: 'PR_BRANCH', value: '$.pullrequest.source.branch.name'],
                [key: 'PR_REPO_HTML', value: '$.repository.links.self.href'],
                [key: 'PR_PROJECT', value: '$.repository.full_name'],
                [key: 'PR_STATE', value: '$.pullrequest.state']
            ],

            tokenCredentialId: 'trigger-token',
            regexpFilterText: '$PR_STATE',
            regexpFilterExpression: 'MERGE'
        )
    }

    environment {
        WORKING_DIR = "${WORKSPACE}/Deployment"
        JOB_REPO = "${PR_REPO_HTML}"
        BITBUCKET_ACCESS_TOKEN = credentials('bitbucket-access-token')
    }

    stages {
        stage ('Delete Merged Branch') {
            steps {
                script {
                    util = load("${WORKSPACE}/groovy/pipelineUtil.groovy")
                    def branch_path = sh (script: "/usr/bin/find ../ -type d -name \"${PR_BRANCH}\"", returnStdout: true)
                    branch_path = branch_path.trim()
                    
                    sh "rm -r \"${branch_path}\""
                    sh "rm -r \"${branch_path}@tmp\""

                    env.FOLDER_NAME = "${JOB_NAME}".split('/').first()
                    def ticketNumber = util.parseTicketNumber(PR_BRANCH)
                    util.cleanMergedBranchReportsFromWebServer(FOLDER_NAME, ticketNumber)
                }
            }
        }
        stage ('Prepare Workspace') {
            environment {
                REPO_SSH = "git@bitbucket.org:${PR_PROJECT}.git"
            }
            steps {
                script {
                    if (!fileExists(WORKING_DIR)) {
                        echo "First time running pipeline. Cloning main branch..."
                        sh "git clone ${REPO_SSH} \"${WORKING_DIR}\""
                    }
                }
                
                dir ("${WORKING_DIR}") {
                    echo "Cleaning project..."
                    sh "git clean -d -f ."
                    echo "Pulling latest version of default branch..."
                    sh "git pull"
                    script {
                        env.COMMIT_HASH = "${sh (script: "git log -n 1 --pretty=format:\"%H\"", returnStdout: true)}"
                    }
                }

                script {
                    echo "Sending \'In Progress\' status to Bitbucket..."
                    util.sendBuildStatus(WORKSPACE, "INPROGRESS", COMMIT_HASH)

                    echo "Identifying Unity version..."
                    env.UNITY_EXECUTABLE = util.getUnityExecutable(WORKSPACE, WORKING_DIR)
                }
            }
        }
        stage ('EditMode Tests') {
            steps {
                echo "Running EditMode tests..."
                dir ("${WORKING_DIR}") {
                    script {
                        util.runUnityTests(UNITY_EXECUTABLE, WORKING_DIR, editMode, false, true)
                    }
                }
            }
        }
        stage ('PlayMode Tests In Editor'){
            steps {
                echo "Running PlayMode tests..."
                dir ("${WORKING_DIR}") {
                    retry (5) {
                        script {
                            util.runUnityTests(UNITY_EXECUTABLE, WORKING_DIR, playMode, false, true)
                        }
                    }
                }
            }
        }
        stage ('Build Project') {
            steps {
                echo "Building Unity project for WebGL..."
                sh "mv Builder.cs \"${WORKING_DIR}/Assets/Editor/\""
                dir("${WORKING_DIR}") {
                    retry (3) {
                        script {
                            util.buildProject(WORKING_DIR, UNITY_EXECUTABLE)
                        }
                    }
                }
            }
        }
        stage ('Deploy Build') {
            steps {
                echo "Deploying build to LTI web server..."

                script {
                    sh "ssh vconadmin@dlx-webhost.canadacentral.cloudapp.azure.com \"sudo mkdir -p /var/www/html/${FOLDER_NAME} \
                    && sudo chown vconadmin:vconadmin /var/www/html/${FOLDER_NAME}\""
                    sh "scp -i C:/Users/ci-catherine/.ssh/vconkey1.pem -rp \"${WORKING_DIR}/Builds/*\" \"vconadmin@dlx-webhost.canadacentral.cloudapp.azure.com:/var/www/html/${FOLDER_NAME}\""
                }
            }
        }
    }

    post {
        success {
            script {
                util.sendBuildStatus(WORKSPACE, "SUCCESSFUL", COMMIT_HASH, "-d")
            }
        }
        failure {
            script {
                util.sendBuildStatus(WORKSPACE, "FAILED", COMMIT_HASH, env.FAILURE_REASON, "-d")
            }
        }
        aborted {
            script {
                util.sendBuildStatus(WORKSPACE, "STOPPED", COMMIT_HASH, "-d")
            }
        }
    }
}