def editMode = "EditMode"
def playMode = "PlayMode"
def util

pipeline {
    agent any

    parameters {
        string(name: 'PR_BRANCH', defaultValue: '', description: '')
        string(name: 'PR_REPO_HTML', defaultValue: '', description: '')
        string(name: 'PR_PROJECT', defaultValue: '', description: '')
        string(name: 'PR_STATE', defaultValue: '', description: '')
    }

    triggers {
        GenericTrigger(
            genericVariables: [
                [key: 'PR_BRANCH', value: '$.pullrequest.source.branch.name'],
                [key: 'PR_REPO_HTML', value: '$.repository.links.self.href'],
                [key: 'PR_PROJECT', value: '$.repository.full_name'],
                [key: 'PR_STATE', value: '$.pullrequest.state']
            ],

            tokenCredentialId: 'trigger-token',
            regexpFilterText: '$PR_STATE',
            regexpFilterExpression: 'MERGED'
        )
    }

    environment {
        WORKING_DIR = "${WORKSPACE}/Deployment"
        JOB_REPO = "${PR_REPO_HTML}"
        BITBUCKET_ACCESS_TOKEN = credentials('bitbucket-access-token')
    }

    stages {
        // Deletes the cloned branch from the build machine after it has been merged into main.
        // This prevents the build machine's storage from capping out.
        stage ('Delete Merged Branch') {
            steps {
                script {
                    util = load("${WORKSPACE}/groovy/pipelineUtil.groovy")
                    def branch_path = sh (script: "/usr/bin/find ../ -type d -name \"${PR_BRANCH}\"", returnStdout: true)
                    branch_path = branch_path.trim()
                    
                    sh "rm -r -f \"${branch_path}\""
                    sh "rm -r -f \"${branch_path}@tmp\""

                    env.FOLDER_NAME = "${JOB_NAME}".split('/').first()
                    def ticketNumber = util.parseTicketNumber(PR_BRANCH)
                    util.cleanMergedBranchReportsFromWebServer(FOLDER_NAME, ticketNumber)
                }
            }
        }
        // Prepares the workspace for the build by cleaning the project, pulling main, and then telling Bitbucket that the
        // build is in progress. It also checks to see if the project's Unity version is downloaded, and installs it if not.
        stage ('Prepare Workspace') {
            environment {
                REPO_SSH = "git@bitbucket.org:${PR_PROJECT}.git"
            }
            steps {
                script {
                    if (!fileExists(WORKING_DIR)) {
                        echo "First time running pipeline. Cloning main branch..."
                        sh "git clone ${REPO_SSH} \"${WORKING_DIR}\""
                    }
                }
                
                dir ("${WORKING_DIR}") {
                    echo "Cleaning project..."
                    sh "git reset --hard HEAD"
                    echo "Pulling latest version of default branch..."
                    sh "git pull"

                    // Getting the full commit hash from git in order to send the build status to Bitbucket.
                    script {
                        env.COMMIT_HASH = "${sh (script: "git log -n 1 --pretty=format:\"%H\"", returnStdout: true)}"
                    }
                }

                script {
                    echo "Sending \'In Progress\' status to Bitbucket..."
                    util.sendBuildStatus(WORKSPACE, "INPROGRESS", COMMIT_HASH)

                    echo "Identifying Unity version..."
                    env.UNITY_EXECUTABLE = util.getUnityExecutable(WORKSPACE, WORKING_DIR)
                }
            }
        }
        // Runs the project's EditMode tests. Unlike the PR pipeline, if any tests fail, this will cause the pipeline to fail.
        // We do not want failing tests on main. 
        stage ('EditMode Tests') {
            steps {
                echo "Running EditMode tests..."
                dir ("${WORKING_DIR}") {
                    script {
                        util.runUnityTests(UNITY_EXECUTABLE, WORKING_DIR, WORKING_DIR, editMode, false, true)
                    }
                }
            }
        }
        // Runs the project's PlayMode tests. Unlike the PR pipeline, if any tests fail, this will cause the pipeline to fail.
        // We do not want failing tests on main. This stage is retried 5 times in case of Unity crashes (which are common).
        stage ('PlayMode Tests In Editor'){
            steps {
                echo "Running PlayMode tests..."
                dir ("${WORKING_DIR}") {
                    retry (5) {
                        script {
                            util.runUnityTests(UNITY_EXECUTABLE, WORKING_DIR, WORKING_DIR, playMode, false, true)
                        }
                    }
                }
            }
        }
        // Builds the project for WebGL.
        stage ('Build Project') {
            steps {
                echo "Building Unity project for WebGL..."
                sh "mv Builder.cs \"${WORKING_DIR}/Assets/Editor/\""

                retry (5) {
                    script {
                        util.buildProject(WORKING_DIR, WORKING_DIR, UNITY_EXECUTABLE)
                    }
                }
            }
        }
        // Deploys the build on our remote web server. 
        stage ('Deploy Build') {
            steps {
                echo "Deploying build to LTI web server..."

                script {
                    sh "ssh vconadmin@dlx-webhost.canadacentral.cloudapp.azure.com \"sudo mkdir -p /var/www/html/${FOLDER_NAME} \
                    && sudo chown vconadmin:vconadmin /var/www/html/${FOLDER_NAME}\""
                    sh "scp -i C:/Users/ci-catherine/.ssh/vconkey1.pem -rp \"${WORKING_DIR}/Builds/*\" \"vconadmin@dlx-webhost.canadacentral.cloudapp.azure.com:/var/www/html/${FOLDER_NAME}\""
                }
            }
        }
    }

    // Sends the build status to Bitbucket. If the build was successful, Bitbucket will link to the deployed project on the web server.
    post {
        success {
            script {
                util.sendBuildStatus(WORKSPACE, "SUCCESSFUL", COMMIT_HASH, "-d")
            }
        }
        failure {
            script {
                util.sendBuildStatus(WORKSPACE, "FAILED", COMMIT_HASH, "-d")
            }
        }
        aborted {
            script {
                util.sendBuildStatus(WORKSPACE, "STOPPED", COMMIT_HASH, "-d")
            }
        }
    }
}