def editMode = "EditMode"
def playMode = "PlayMode"
def util
def main = "main"
def dlxList = ["Public Health Inspection", "Paramedic Ambulance", "Trades Electrical", "pipeline-test"]


pipeline {
    agent any

    parameters {
        string(name: 'PR_BRANCH', defaultValue: '', description: '')
        string(name: 'PR_DESTINATION_BRANCH', defaultValue: '', description: '')
        string(name: 'PR_REPO_HTML', defaultValue: '', description: '')
        string(name: 'PR_REPO_NAME', defaultValue: '', description: '')
        string(name: 'PR_COMMIT', defaultValue: '', description: '')
        string(name: 'PR_PROJECT', defaultValue: '', description: '')
        string(name: 'PR_STATE', defaultValue: '', description: '')
    }

    triggers {
        GenericTrigger(
            genericVariables: [
                [key: 'PR_BRANCH', value: '$.pullrequest.source.branch.name'],
                [key: 'PR_DESTINATION_BRANCH', value: '$.pullrequest.destination.branch.name'],
                [key: 'PR_REPO_HTML', value: '$.repository.links.self.href'],
                [key: 'PR_REPO_NAME', value: '$.repository.name'],
                [key: 'PR_COMMIT', value: '$.pullrequest.source.commit.hash'],
                [key: 'PR_PROJECT', value: '$.repository.full_name'],
                [key: 'PR_STATE', value: '$.pullrequest.state']
            ],

            tokenCredentialId: 'trigger-token',
            regexpFilterText: '$PR_STATE',
            regexpFilterExpression: 'MERGED'
        )
    }

    environment {
        CI_PIPELINE = "false"
        PROJECT_DIR = "${WORKSPACE}/Deployment"
        REPORT_DIR = "${WORKSPACE}/Deployment"
        JOB_REPO = "${PR_REPO_HTML}"
        BITBUCKET_ACCESS_TOKEN = credentials('bitbucket-access-token')
        DESTINATION_BRANCH = "${PR_DESTINATION_BRANCH}"
    }

    stages {
        // Deletes the cloned branch from the build machine after it has been merged into main.
        // This prevents the build machine's storage from capping out.
        stage ('Delete Merged Branch') {
            steps {
                script {
                    // Aborts the pipeline if it's not merging into the main.
                    if(!env.DESTINATION_BRANCH.equals(main)){
                        env.FAILURE_REASON = "Not merging to the main branch. Exiting the pipeline..."
                        currentBuild.result = 'ABORTED'
                        error(env.FAILURE_REASON)
                    }

                    util = load("${WORKSPACE}/groovy/pipelineUtil.groovy")
                    def branch_path = sh (script: "/usr/bin/find ../ -type d -name \"${PR_BRANCH}\"", returnStdout: true)
                    branch_path = branch_path.trim()
                    if(!branch_path.isEmpty()){
                        sh "rm -r -f \"${branch_path}\""
                        sh "rm -r -f \"${branch_path}@tmp\""
                    }

                    env.FOLDER_NAME = "${JOB_NAME}".split('/').first()
                    def ticketNumber = util.parseTicketNumber(PR_BRANCH)
                    util.cleanMergedBranchReportsFromWebServer(FOLDER_NAME, ticketNumber)
                }
            }
        }
        // Prepares the workspace for the build by cleaning the project, pulling main, and then telling Bitbucket that the
        // build is in progress. It also checks to see if the project's Unity version is downloaded, and installs it if not.
        stage ('Prepare Workspace') {
            environment {
                REPO_SSH = "git@bitbucket.org:${PR_PROJECT}.git"
            }
            steps {
                script {
                    // Getting the full commit hash from git in order to send the build status to Bitbucket.
                    script {
                        env.COMMIT_HASH = util.getFullCommitHash(WORKSPACE, PR_COMMIT)
                        //env.COMMIT_HASH = "${sh (script: "git log -n 1 --pretty=format:\"%H\"", returnStdout: true)}"
                    }

                    if (!fileExists("${PROJECT_DIR}")) {
                        echo "First time running pipeline. Cloning main branch..."
                        sh "git clone ${REPO_SSH} \"${PROJECT_DIR}\""
                    }
                }
                
                dir ("${PROJECT_DIR}") {
                    // Ensure it stays in the main branch
                    echo "Checkout to the main branch..."
                    sh "git checkout ${main}"
                    echo "Cleaning project..."
                    sh "git reset --hard HEAD"
                    echo "Pulling latest version of default branch..."
                    sh "git pull"
                }

                script {
                    echo "Sending \'In Progress\' status to Bitbucket..."
                    util.sendBuildStatus(WORKSPACE, "INPROGRESS", COMMIT_HASH)

                    echo "Identifying Unity version..."
                    env.UNITY_EXECUTABLE = util.getUnityExecutable(WORKSPACE, PROJECT_DIR)
                }

                // Initial running the project on Unity Editor    
                echo "Running Unity in batch mode to setup initial files..."
                dir ("${PROJECT_DIR}") {
                    script {
                        def logFile = "${PROJECT_DIR}/batch_mode_execution.log"
                        def flags = "-batchmode -nographics -projectPath \"${PROJECT_DIR}\" -executeMethod \"Packages.Rider.Editor.RiderScriptEditor.SyncSolution\" -logFile \"${logFile}\" -quit "
                        
                        echo "Flags set to: ${flags}"
                        
                        // Execute Unity in batch mode with memory settings
                        def exitCode = sh(script: """\"${env.UNITY_EXECUTABLE}\" ${flags}""", returnStatus: true)
                        
                        // Handle exit code
                        if (exitCode != 0) {
                            sh "exit ${exitCode}"
                        }    
                    }
                }
            }
        }
        //linting stage to check formatting
        stage('Linting')
        {
            steps{
                //Linting
                echo "running lint script"
                script {
                    echo "Parameters for bash: ${WORKSPACE}/Linting.bash ${PROJECT_DIR} ${REPORT_DIR}"
                    def exitCode = sh script: "sh \'${WORKSPACE}/Bash/Linting.bash\' \'${PROJECT_DIR}\' \'${REPORT_DIR}\'", returnStatus: true 
                    echo "After bash call, exit code: ${exitCode}"
                    //handle exit code here no reports to be sent, since this for deployment and not PRs
                    if(exitCode != 0)
                    {
                        //in this case we set build result to failure since we don't want to deploy bad code
                        catchError(buildResult: 'SUCCESS', stageResult: 'FAILURE'){
                            error("Linting failed with exit code: ${exitCode}") //we exit no matter what on error code != 0
                        }
                    }
                }
            }
        }
        // Runs the project's EditMode tests. Unlike the PR pipeline, if any tests fail, this will cause the pipeline to fail.
        // We do not want failing tests on main. 
        stage ('EditMode Tests') {
            steps {
                echo "Running EditMode tests..."
                dir ("${PROJECT_DIR}") {
                    script {
                        util.runUnityTests(UNITY_EXECUTABLE, REPORT_DIR, PROJECT_DIR, editMode, false, true)
                    }
                }
            }
        }
        // Runs the project's PlayMode tests. Unlike the PR pipeline, if any tests fail, this will cause the pipeline to fail.
        // We do not want failing tests on main. This stage is retried 5 times in case of Unity crashes (which are common).
        stage ('PlayMode Tests In Editor'){
            steps {
                echo "Running PlayMode tests..."
                dir ("${PROJECT_DIR}") {
                    retry (5) {
                        script {
                            util.runUnityTests(UNITY_EXECUTABLE, REPORT_DIR, PROJECT_DIR, playMode, false, true)
                        }
                    }
                }
            }
        }
        // Builds the project for WebGL.
        stage ('Build Project') {
            steps {
                echo "Building Unity project for WebGL..."
                sh "mkdir -p \"${PROJECT_DIR}/Assets/Editor/\"" //The following line assumed this folder exists in every project, adding check to ensure it does.
                sh "mv Builder.cs \"${PROJECT_DIR}/Assets/Editor/\""

                retry (5) {
                    script {
                        util.buildProject(REPORT_DIR, PROJECT_DIR, UNITY_EXECUTABLE)
                    }
                }
            }
        }
        // Deploys the build on our remote web server. 
        stage ('Deploy Build') {
            steps {
                echo "Deploying build to LTI web server..."

                script {
                    sh "ssh vconadmin@dlx-webhost.canadacentral.cloudapp.azure.com \"sudo mkdir -p /var/www/html/${FOLDER_NAME} \
                    && sudo chown vconadmin:vconadmin /var/www/html/${FOLDER_NAME}\""
                    sh "scp -i C:/Users/ci-catherine/.ssh/vconkey1.pem -rp \"${PROJECT_DIR}/Builds/*\" \"vconadmin@dlx-webhost.canadacentral.cloudapp.azure.com:/var/www/html/${FOLDER_NAME}\""
                    //call script to copy that into the deployed server
                    sh "ssh vconadmin@dlx-webhost.canadacentral.cloudapp.azure.com 'bash ~/ShellScripts/CopyBuildToServer.sh /var/www/html/${FOLDER_NAME} ~/DLX-Server/devopsdlxserver/public/'"
                }
                
                script {
                    if (dlxList.contains(PR_REPO_NAME)) {
                        echo "Deploying build to eConestoga dlx web server..."

                        // Create the directory and update the file permission
                        sh "ssh -i C:/Users/ci-catherine/.ssh/vconkey1.pem vconadmin@dlx-econestoga.canadacentral.cloudapp.azure.com \"sudo mkdir -p /var/www/html/${FOLDER_NAME} && sudo chown vconadmin:vconadmin /var/www/html/${FOLDER_NAME}\""
                        // Secured copy all WebGL components to the econestoga dlx server
                        sh "scp -i C:/Users/ci-catherine/.ssh/vconkey1.pem -rp \"${PROJECT_DIR}/Builds/*\" \"vconadmin@dlx-econestoga.canadacentral.cloudapp.azure.com:/var/www/html/${FOLDER_NAME}\""
                        //call script to update the WebGL "index.html"
                        sh "ssh vconadmin@dlx-econestoga.canadacentral.cloudapp.azure.com 'bash ~/ShellScripts/UpdateBuildURL.sh /var/www/html/${FOLDER_NAME}'"
                        
                    }
                }
            }
        }
    }

    // Sends the build status to Bitbucket. If the build was successful, Bitbucket will link to the deployed project on the web server.
    post {
        success {
            script {
                util.sendBuildStatus(WORKSPACE, "SUCCESSFUL", COMMIT_HASH, "-d")
            }
        }
        failure {
            script {
                util.sendBuildStatus(WORKSPACE, "FAILED", COMMIT_HASH, "-d")
            }
        }
        aborted {
            script {
                util.sendBuildStatus(WORKSPACE, "STOPPED", COMMIT_HASH, "-d")
            }
        }
    }
}